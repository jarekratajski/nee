buildscript {
    ext {
        kotlin_version = '1.3.71'
        liquibase_version='3.6.1'
        vavr_version='0.10.2'
        h2_version='1.4.193'
        kotest_version = '4.1.1'
        ktor_version = '1.2.4'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.5.0")
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: "maven-publish"
    apply plugin: "jacoco"
    apply plugin: "io.gitlab.arturbosch.detekt"

    group 'pl.setblack'
    version '0.2.0-SNAPSHOT'

    dependencies {
        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
            allWarningsAsErrors = true
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
        }
    }
    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = false
            csv.enabled = false
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    detekt {
        failFast = true // fail build on any finding
        buildUponDefaultConfig = true // preconfigure defaults
        config = files("${rootDir}/config/detekt.yml")
        //baseline = file("$projectDir/config/baseline.xml")
        reports {
            html.enabled = true // observe findings in your browser with structure and code snippets
            xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
            txt.enabled = true // similar to the console output, contains issue signature to manually edit baseline files
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

}


task generateMergedReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

